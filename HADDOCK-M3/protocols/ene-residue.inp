!$Revision: 2.2 $
!$Date: 2010/02/10 16:01:54 $
!$RCSfile: ene-residue.inp,v $

! energy.inp:
! calculate statistics for all energy terms
!
!     ***********************************
!     * Authors and copyright:           *
!     * Alexandre Bonvin, Utrecht        *
!     * No warranty implied or expressed *
!     * All rights reserved              *
!     * (adapted from the ARIA distribu- *
!     * tion of Nilges and Linge)        *
!     ************************************

@RUN:protocols/initialize.cns(iteration=$iteration;)

@NEWIT:iteration.cns(iteration=$iteration;)

@RUN:run.cns(
iteration =$iteration;
filenames =$filenames;
data      =$data;
iterations=$iterations;
saprotocol=$saprotocol;
refine    =$refine;
toppar    =$toppar;
analysis  =$analysis;
)

!reads in structure, coordinates and parameter files
@RUN:protocols/read_struc.cns

@PREVIT:file.cns(filenames=$filenames)

evaluate ($count = 1)
evaluate ($nstruc1 = 0)
do (store1 = 0) (all)
while ($count le $Iterations.anastruc) loop loop00
  evaluate ($file = $filenames.bestfile_$count)
  coor init end
  if ($file ne "") then
    coor @@$file
    dele sele=(not known) end
    evaluate ($nstruc1 = $nstruc1 + 1)
  end if
  evaluate ($count = $count + 1)
end loop loop00

!set the energy flags:
@@RUN:protocols/flags_new.cns(Iteration=$Iteration; Data=$Data; )
flags exclude * include vdw elec end

set display=NEWIT:analysis/ene-residue.disp end

@PREVIT:file.cns(filenames=$filenames)

evaluate ($count = 1)
evaluate ($nstruc1 = 0)
do (store1 = 0) (all)
while ($count le $Iterations.anastruc) loop loop0
  evaluate ($file = $filenames.bestfile_$count)
  coor init end
  if ($file ne "") then
    coor @@$file
    evaluate ($nstruc1 = $nstruc1 + 1)
    eval($nchain1 = 0)
    while ($nchain1 < $data.ncomponents) loop nloop1
      eval($nchain1 = $nchain1 + 1)
      do (store1 = 1) (byres (segid $Toppar.prot_segid_$nchain1 and
                              (not segid $Toppar.prot_segid_$nchain1) around 5.0))
    end loop nloop1
  end if
  evaluate ($count = $count + 1)
end loop loop0

for $id in id (attr store1 = 1 and tag) loop loop2
  show (resid) (id $id)
  evaluate ($ires = $result)
  show (resn) (id $id)
  evaluate ($nres = $result)
  show (segid) (id $id)
  evaluate ($iseg = $result)
  igroup interaction (segid $iseg and resid $ires) (not segid $iseg and not ((resn WAT or resn HOH or resn TIP3) or resn DMSO)) end
  display #
  display #
  display #Residue $nres $ires $iseg - intermolecular energies
  display #file Etot Evdw Eelec

  evaluate ($sum_tot = 0)
  evaluate ($sumsq_tot = 0)
  evaluate ($sum_vdw = 0)
  evaluate ($sumsq_vdw = 0)
  evaluate ($sum_elec = 0)
  evaluate ($sumsq_elec = 0)

  @PREVIT:file.cns(filenames=$filenames)

  evaluate ($count = 1)
  evaluate ($nstruc1 = 0)
  while ($count le $Iterations.anastruc) loop loop3
    evaluate ($file = $filenames.bestfile_$count)
    coor init end
    if ($file ne "") then
      coor @@$file
      evaluate ($nstruc1 = $nstruc1 + 1)
      energy end
      evaluate ($sum_tot = $sum_tot + $ener)
      evaluate ($sumsq_tot = $sumsq_tot + $ener**2)
      evaluate ($sum_vdw = $sum_vdw + $vdw)
      evaluate ($sumsq_vdw = $sumsq_vdw + $vdw**2)
      evaluate ($sum_elec = $sum_elec + $elec)
      evaluate ($sumsq_elec = $sumsq_elec + $elec**2)

      display # $file $ener $vdw $elec

   end if
   evaluate ($count = $count + 1)
  end loop loop3

  evaluate ($mean_tot = $sum_tot / $nstruc1)
  evaluate ($stdev_tot = sqrt(max(0.0,($sumsq_tot - $nstruc1*$mean_tot**2))/ $nstruc1))
  evaluate ($mean_vdw = $sum_vdw / $nstruc1)
  evaluate ($stdev_vdw = sqrt(max(0.0,($sumsq_vdw - $nstruc1*$mean_vdw**2))/ $nstruc1))
  evaluate ($mean_elec = $sum_elec / $nstruc1)
  evaluate ($stdev_elec = sqrt(max(0.0,($sumsq_elec - $nstruc1*$mean_elec**2))/ $nstruc1))

  display # mean values for interaction with residue $nres $ires $iseg
  display # $nres $ires $iseg : Etot   $mean_tot (+/- $stdev_tot ) [kcal/Mol]
  display # $nres $ires $iseg : Evdw   $mean_vdw (+/- $stdev_vdw ) [kcal/Mol]
  display # $nres $ires $iseg : Eelec  $mean_elec (+/- $stdev_elec ) [kcal/Mol]

end loop loop2

close NEWIT:analysis/ene-residue.disp end

evaluate ($outdis  = "NEWIT:analysis/ENE_DONE")
set display=$outdis end
display DONE

stop
